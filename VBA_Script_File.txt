Sub Stock_Analysis():
    
    'Set variable for the purposes of looping through all worksheets.
    Dim w As Worksheet
    
    'Set variables.
    Dim ticker As String
    Dim yearly_change As Double
    Dim percent_change As String
    Dim total_volume As Double
    
    Dim opening_price As Double
    Dim closing_price As Double
    
    'Create a variable for the row to be used when populating the answers.
    Dim answers_row As Integer
    
    Dim greatest_increase As Double
    Dim greatest_decrease As Double
    Dim greatest_total_volume As Double
    
    Dim last_row As Long
    
    'When we loop through each worksheet:
    For Each w In Worksheets
    
        'Print column headers.
        w.Range("I1").Value = "Ticker"
        w.Range("J1").Value = "Yearly Change"
        w.Range("K1").Value = "Percent Change"
        w.Range("L1").Value = "Total Stock Volume"
        w.Range("P1").Value = "Ticker"
        w.Range("Q1").Value = "Value"
        'Print row headers.
        w.Range("O2").Value = "Greatest % Increase"
        w.Range("O3").Value = "Greatest % Decrease"
        w.Range("O4").Value = "Greatest Total Volume"
    
        'This has to be used in order for the For loop to stop at the last row with data.
        last_row = w.Cells(Rows.Count, 1).End(xlUp).Row
        
        'Set total_value to equal 0 for the purposes of the For loop.
        total_volume = 0
        
        answers_row = 2
        
        'opening_price begins as the opening price of the first ticker.
        opening_price = w.Cells(2, 3).Value
    
        'Loop through all the rows in the worksheet.
        For i = 2 To last_row
            
            'When the for loop comes across a ticker value different than the one preceding it, then:
            If w.Cells(i + 1, 1).Value <> w.Cells(i, 1).Value Then
        
                'Get ticker name.
                ticker = w.Cells(i, 1).Value
            
                'Get closing_price of ticker.
                closing_price = w.Cells(i, 6).Value
            
                'Figure out yearly_change.
                yearly_change = closing_price - opening_price
            
                'Figure out percent_change.
                percent_change = Format((yearly_change / opening_price), "Percent")
            
                'Add up the last volume value to the total_value sum.
                total_volume = total_volume + w.Cells(i, 7).Value
            
                'Place the ticker name in cell(I & answers_row).
                w.Range("I" & answers_row).Value = ticker
            
                'Place the yearly_change in cell(J & answers_row).
                w.Range("J" & answers_row).Value = yearly_change
            
                    'If yearly_change is positive, then...
                If yearly_change > 0 Then
            
                    'Paint the cell green.
                    w.Range("J" & answers_row).Interior.ColorIndex = 4
                
                'Otherwise...
                Else
            
                    'Paint the cell red.
                    w.Range("J" & answers_row).Interior.ColorIndex = 3
                
                End If
            
                'Place the percent_change in cell(K & answers_row).
                w.Range("K" & answers_row).Value = percent_change
    
                'Place the total_value sum for the ticker name in cell(L & answers_row).
                w.Range("L" & answers_row).Value = total_volume
    
                'Move the answers_row down one row for the next ticker name.
                answers_row = answers_row + 1
    
                'Reset total_volume back to 0 for the next ticker name.
                total_volume = 0
                
                'Change the opening_price to the opening price of the next ticker.
                opening_price = w.Cells(i + 1, 3).Value
            
            Else
        
                'If there is no change in ticker name, then only the volume of the ticker is added to the total_volume sum.
                total_volume = total_volume + w.Cells(i, 7).Value
            
            End If
        
        Next i
        
        'Find greatest_increase in the Yearly Change column.
        greatest_increase = Application.WorksheetFunction.Max(w.Range("K2" & ":" & "K" & last_row))
            
        'Put the value of the greatest_increase in cell (P2).
        w.Cells(2, 17).Value = Format((greatest_increase), "Percent")
        
        'Find corresponding ticker name of the greatest_increase and put it in cell P2.
        w.Cells(2, 16).Value = Application.WorksheetFunction.Index(w.Range("I2" & ":" & "I" & last_row), WorksheetFunction.Match(w.Range("Q2").Value, w.Range("K2" & ":" & "K" & last_row), 0))

        'Find the greatest_decrease in the Yearly Change column.
        greatest_decrease = Application.WorksheetFunction.Min(w.Range("K2" & ":" & "K" & last_row))
        
        'Put the value of the greatest_decrease in cell (P3).
        w.Cells(3, 17).Value = Format((greatest_decrease), "Percent")
        
        'Find corresponding ticker name of the greatest_decrease and put it in cell P3.
        w.Cells(3, 16).Value = Application.WorksheetFunction.Index(w.Range("I2" & ":" & "I" & last_row), WorksheetFunction.Match(w.Range("Q3").Value, w.Range("K2" & ":" & "K" & last_row), 0))

        'Find the greatest_total_volume in the Total Stock Volume column.
        greatest_total_volume = Application.WorksheetFunction.Max(w.Range("L2" & ":" & "L" & last_row))
        
        'Put the value of the greatest_total_volume in cell (P4).
        w.Cells(4, 17).Value = greatest_total_volume
        
        'Find corresponding ticker name of the greatest_total_volume and put it in cell P4.
        w.Cells(4, 16).Value = Application.WorksheetFunction.Index(w.Range("I2" & ":" & "I" & last_row), WorksheetFunction.Match(w.Range("Q4").Value, w.Range("L2" & ":" & "L" & last_row), 0))
        
    Next w
    
End Sub






